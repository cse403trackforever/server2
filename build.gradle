buildscript {
    ext {
        kotlin_version = '1.2.41' // Required for Kotlin integration
        spring_boot_version = '2.0.2.RELEASE'
        junit_verison = '5.2.0'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version" // Required for Kotlin integration
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version" // See https://kotlinlang.org/docs/reference/compiler-plugins.html#kotlin-spring-compiler-plugin
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
    }
}

plugins {
    id "com.github.kt3k.coveralls" version "2.8.2"
}

apply plugin: 'kotlin'
apply plugin: "kotlin-spring"
apply plugin: 'org.springframework.boot'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'

// Configure jacoco
jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

// Java target version
sourceCompatibility = 1.8
group = 'com.trackforever'
version = '0.0.1-SNAPSHOT'

repositories {
    jcenter()
    mavenCentral()
}

test {
    // Enable JUnit 5 (Gradle 4.6+).
    useJUnitPlatform()

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

sourceSets {
    main.kotlin.srcDirs += 'main'
    main.resources.srcDirs += 'main'
    test.kotlin.srcDirs += 'test'
    test.resources.srcDirs += 'test'
}

jar {
    baseName = 'gs-securing-web'
    version =  '0.1.0'
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-data-mongodb')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.jetbrains.kotlin:kotlin-stdlib-jdk8')
    compile('org.jetbrains.kotlin:kotlin-reflect')
//    compile('com.beust:klaxon:3.0.4')
    compile('de.flapdoodle.embed:de.flapdoodle.embed.mongo')
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.+"

    testCompile('org.jetbrains.kotlin:kotlin-test')
    testCompile('org.jetbrains.kotlin:kotlin-test-junit')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile("org.junit.jupiter:junit-jupiter-api:$junit_verison")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:$junit_verison")
}